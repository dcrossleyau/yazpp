AC_INIT(configure.in)
AM_INIT_AUTOMAKE("yaz++",0.7.11)

AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_HEADER_STDC
AM_DISABLE_SHARED
AM_PROG_LIBTOOL

YAZ_INIT(threads,2.0.12)

AC_CHECK_FUNCS(setrlimit getrlimit)
dnl
dnl ----- libXSLT
AC_SUBST(XSLT_LIBS)
AC_SUBST(XSLT_CFLAGS)
xsltdir=yes
AC_ARG_WITH(xslt, [  --with-xslt[=PREFIX]      use libxslt in PREFIX],[xsltdir=$withval])
if test "$xsltdir" = "yes"; then
	for d in /usr /usr/local; do
		if test -x $d/bin/xslt-config; then
			xsltdir=$d
		fi
	done
fi
if test "$xsltdir" != "no"; then
	AC_MSG_CHECKING(for libXSLT)
	if test -x $xsltdir/bin/xslt-config; then
		XSLT_LIBS=`$xsltdir/bin/xslt-config --libs`
		LIBS="$XSLT_LIBS $LIBS"
		XSLT_CFLAGS=`$xsltdir/bin/xslt-config --cflags`
		XSLT_VER=`$xsltdir/bin/xslt-config --version`
		AC_MSG_RESULT($XSLT_VER)
		AC_DEFINE(HAVE_XSLT)
	else
		AC_MSG_RESULT(Not found)
	fi
fi

dnl
dnl ----- DOCBOOK DTD
AC_SUBST(DTD_DIR)
AC_ARG_WITH(dtd, [  --with-dtd[=DIR]          use docbookx.dtd in DIR],
[
   if test -f "$withval/docbookx.dtd"; then
      DTD_DIR=$withval
   fi
],[
   AC_MSG_CHECKING(for docbookx.dtd)
   for d in /usr/share/sgml/docbook/dtd/xml/4.1.2 \
         /usr/share/sgml/docbook/xml-dtd-4.1.2* \
         /usr/share/sgml/docbook/xml-dtd-4.1 \
         /usr/share/sgml/docbook/dtd/xml/4.0 \
	    /usr/lib/sgml/dtd/docbook-xml 
   do
     if test -f $d/docbookx.dtd; then
       AC_MSG_RESULT($d)
       DTD_DIR=$d
       break
     fi
   done
   if test -z "$DTD_DIR"; then
      AC_MSG_RESULT(Not found)
   fi
])
AC_SUBST(DSSSL_DIR)
AC_ARG_WITH(dsssl,[  --with-dsssl[=DIR]        use DSSSL in DIR/{html,print}/docbook.dsl],
[
   if test -f "$withval/html/docbook.dsl"; then
      DSSSL_DIR=$withval
   fi
],[
   AC_MSG_CHECKING(for docbook.dsl)
   for d in /usr/share/sgml/docbook/stylesheet/dsssl/modular \
            /usr/share/sgml/docbook/dsssl-stylesheets-1.* \
            /usr/lib/sgml/stylesheet/dsssl/docbook/nwalsh 
   do
     if test -f $d/html/docbook.dsl; then
       AC_MSG_RESULT($d)
       DSSSL_DIR=$d
       break
     fi
   done
   if test -z "$DSSSL_DIR"; then
      AC_MSG_RESULT(Not found)
   fi
])
dnl 

AC_ARG_ENABLE(zoom,[  --disable-zoom          disable ZOOM (for old C++ compilers)],[enable_zoom=$enableval],[enable_zoom=yes])
AM_CONDITIONAL(ZOOM, test $enable_zoom = "yes")

AC_SUBST(YAZPP_SRC_ROOT)
AC_SUBST(YAZPP_BUILD_ROOT)
YAZPP_SRC_ROOT=`cd ${srcdir}; pwd`
YAZPP_BUILD_ROOT=`pwd`

AC_OUTPUT([
	Makefile
	src/Makefile
	include/Makefile
	include/yaz++/Makefile
	zlint/Makefile
	yaz++-config
	zoom/Makefile
	doc/Makefile
	doc/yaz++.xml
	doc/yazprint.dsl doc/yazphp.dsl doc/yazhtml.dsl
        doc/tkl.xsl
	etc/Makefile
],[sed s%yaz_echo_source=yes%yaz_echo_source=no%g < yaz++-config >src/yaz++-config && chmod +x yaz++-config src/yaz++-config])
